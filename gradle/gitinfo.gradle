import org.eclipse.jgit.storage.file.*
import org.eclipse.jgit.api.*
import org.eclipse.jgit.lib.*
import org.eclipse.jgit.submodule.*

buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    classpath 'org.eclipse.jgit:org.eclipse.jgit:5.9.0.202009080501-r'
    classpath 'commons-codec:commons-codec:1.6'
  }
}

/** Return the current checkout's status. */
def gitStatus(dir) {
  def status = [
    abbrRev: "[unset]",
    fullRev: "[unset]",
    clean  : false,
    branch : "[unset]",
    id     : "[unset]"
  ]

  try {
    Repository repo = new FileRepositoryBuilder()
        .findGitDir(file(dir))
        .build()
    Git git = new Git(repo)

    Map<String, SubmoduleStatus> submodules = git.submoduleStatus().call()
    gradle.taskGraph.whenReady { graph ->
      submodules.each { path, st ->
        if (st.type == SubmoduleStatusType.REV_CHECKED_OUT && rootProject.ext.devMode) {
          // Allow new commits in submodules while assembling development-mode stuff.
          return
        }
        if (st.type != SubmoduleStatusType.INITIALIZED) {
          throw new GradleException("Suspicious submodule status: ${path} ${st.type}")
        }
      }
    }

    Ref head = repo.findRef("HEAD")

    Status st = git.status().call()
    ObjectId commit = head.getObjectId()
    status.fullRev = commit.name()
    status.abbrRev = commit.abbreviate(8).name()
    status.clean = st.clean
    status.branch = repo.branch

    status.id = status.branch + "/" + status.abbrRev
    if (!status.clean) {
      status.id += '-dirty'

      logger.warn("Git status indicates dirty checkout (run with --info for verbose git status)")
      logger.info("Git status is: added={}, changed={}, conflicting={}, ignored={}, missing={}, modified={}, removed={}, untracked={}, untrackedFolders={}",
          st.added,
          st.changed,
          st.conflicting,
          st.ignoredNotInIndex,
          st.missing,
          st.modified,
          st.removed,
          st.untracked,
          st.untrackedFolders)
    }
  } catch (Exception e) {
    throw e // Break the build if not under a repo.
  }

  return status
}

ext.gitStatus = gitStatus(".")